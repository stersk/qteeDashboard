
&НаСервере
Процедура КомандаЗарегистрироватьКОбработкеНаСервере()
	УзелДляРегистрации = ПланыОбмена.U_ВыгрузкаОнлайнСтатистики.ПолучитьУзел();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	U_ОнлайнПлатеж.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.U_ОнлайнПлатеж КАК U_ОнлайнПлатеж
	|ГДЕ
	|	U_ОнлайнПлатеж.Сумма > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказКлиента.Ссылка
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	НЕ ЗаказКлиента.ПометкаУдаления
	|   И НЕ ЗаказКлиента.U_НомерДекларации = """"";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл 
		ПланыОбмена.ЗарегистрироватьИзменения(УзелДляРегистрации, Выборка.Ссылка);
	КонецЦикла;
	
	ОбновитьКоличествоЗарегистрированных();
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗарегистрироватьКОбработке(Команда)
	КомандаЗарегистрироватьКОбработкеНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомандаобработатьВсеНаСервере()
	Результат = U_ЗагрузкаВыгрузкаДанных.ВыгрузитьСтатистику();
	ОбновитьКоличествоЗарегистрированных();
	
	Если Результат.Успех Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Выгрузка завершена");
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрСоединить(Результат.Ошибки, Символы.ПС));
	КонецЕсли;
	
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура КомандаобработатьВсе(Команда)
	КомандаобработатьВсеНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьКоличествоЗарегистрированных();
	
	Список.Параметры.УстановитьЗначениеПараметра("Узел", ПланыОбмена.U_ВыгрузкаОнлайнСтатистики.ПолучитьУзел());
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличествоЗарегистрированных()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Узел", ПланыОбмена.U_ВыгрузкаОнлайнСтатистики.ПолучитьУзел());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	U_ОнлайнПлатежИзменения.Узел КАК Узел,
	|	U_ОнлайнПлатежИзменения.Ссылка КАК Количество
	|ПОМЕСТИТЬ втВсеТаблицы
	|ИЗ
	|	Документ.U_ОнлайнПлатеж.Изменения КАК U_ОнлайнПлатежИзменения
	|ГДЕ
	|	U_ОнлайнПлатежИзменения.Узел = &Узел
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказКлиентаИзменения.Узел,
	|	ЗаказКлиентаИзменения.Ссылка
	|ИЗ
	|	Документ.ЗаказКлиента.Изменения КАК ЗаказКлиентаИзменения
	|ГДЕ
	|	ЗаказКлиентаИзменения.Узел = &Узел
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втВсеТаблицы.Узел КАК Узел,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ втВсеТаблицы.Количество) КАК Количество
	|ИЗ
	|	втВсеТаблицы КАК втВсеТаблицы
	|
	|СГРУППИРОВАТЬ ПО
	|	втВсеТаблицы.Узел";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КоличествоКОбработке = Выборка.Количество;
	Иначе
		КоличествоКОбработке = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьНастройки(Команда)
	ОткрытьФорму("ПланОбмена.U_ВыгрузкаОнлайнСтатистики.Форма.U_НастройкиПодсистемы", Новый Структура, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура УдалитьРегистрациюВсех(Команда)
	УдалитьРегистрациюВсехНаСервере();
КонецПроцедуры

&НаСервере
Процедура УдалитьРегистрациюВсехНаСервере()
	ПланыОбмена.УдалитьРегистрациюИзменений(ПланыОбмена.U_ВыгрузкаОнлайнСтатистики.ПолучитьУзел());
	
	ОбновитьКоличествоЗарегистрированных();
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОдиночноеРегистрирование(Команда)
	ОткрытьФорму("ПланОбмена.U_ПрисоедененныеФайлыКОбработке.Форма.ФормаРегистрацииИзмененийКартинокНоменклатуры", Новый Структура, ЭтотОбъект,,,,Новый ОписаниеОповещения("РегистрацияИзменений_Окончание", ЭтотОбъект, Новый Структура), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияИзменений_Окончание(Результат, Параметры) Экспорт 
	ОбновитьКоличествоЗарегистрированных();
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновитьДанные(Команда)
	ОбновитьКоличествоЗарегистрированных();
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьБаланс(Команда)
	Результат = U_ЗагрузкаВыгрузкаДанных.ПолучитьБаланс();
	Если Результат.Успех Тогда
		ТекстСообщения = СтрШаблон("Баланс: %1 грн. (актуальность %2)", Результат.Данные.Значение, Результат.Данные.Дата);
	Иначе
		ТекстСообщения = "Ошибка получения данных: " + Символы.ПС + СокрЛП(СтрСоединить(Результат.Ошибки, Символы.ПС));
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
КонецПроцедуры

