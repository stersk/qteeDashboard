#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	КлючАпиМонобанк 					= U_ОнлайнСтатистикаПовтИсп.ПарольБанк_Получить();
	ДатаПоследнейСинхронизацииМонобанк 	= U_ОнлайнСтатистикаПовтИсп.ДатаПоследнейЗагрузки_Получить();
	ИдентификаторМонобанк 				= U_ОнлайнСтатистикаПовтИсп.Идентификатор_Получить();
	АдресВебХукаМонобанк				= U_ОнлайнСтатистикаПовтИсп.АдресВебХукаМонобанк_Получить();
	
	АдресСерверСтатистики				= U_ОнлайнСтатистикаПовтИсп.АдресСерверСтатистики_Получить();
	ПортСерверСтатистики				= U_ОнлайнСтатистикаПовтИсп.ПортСерверСтатистики_Получить();
	ПользовательСерверСтатистики		= U_ОнлайнСтатистикаПовтИсп.ПользовательСерверСтатистики_Получить();
	ПарольСерверСтатистики				= U_ОнлайнСтатистикаПовтИсп.ПарольСерверСтатистики_Получить();
	HTTPSСерверСтатистики				= U_ОнлайнСтатистикаПовтИсп.HTTPSСерверСтатистики_Получить();
	
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОк(Команда)
	U_ОнлайнСтатистикаПовтИсп.ПарольБанк_Сохранить(КлючАпиМонобанк);
	U_ОнлайнСтатистикаПовтИсп.ДатаПоследнейЗагрузки_Сохранить(ДатаПоследнейСинхронизацииМонобанк);
	U_ОнлайнСтатистикаПовтИсп.Идентификатор_Сохранить(ИдентификаторМонобанк);
	U_ОнлайнСтатистикаПовтИсп.АдресВебХукаМонобанк_Сохранить(АдресВебХукаМонобанк);
	
	U_ОнлайнСтатистикаПовтИсп.АдресСерверСтатистики_Сохранить(АдресСерверСтатистики);
	U_ОнлайнСтатистикаПовтИсп.ПортСерверСтатистики_Сохранить(ПортСерверСтатистики);
	U_ОнлайнСтатистикаПовтИсп.ПользовательСерверСтатистики_Сохранить(ПользовательСерверСтатистики);
	U_ОнлайнСтатистикаПовтИсп.ПарольСерверСтатистики_Сохранить(ПарольСерверСтатистики);
	U_ОнлайнСтатистикаПовтИсп.HTTPSСерверСтатистики_Сохранить(HTTPSСерверСтатистики);
		
	Модифицированность = Ложь;
	Закрыть();
КонецПроцедуры
	
&НаКлиенте
Процедура КомандаОтмена(Команда)
	Модифицированность = Ложь;
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВебХук()
	Результат = U_ЗагрузкаВыгрузкаДанных.УстановитьВебХукМоноБанк(КлючАпиМонобанк);
	Если Результат.Успех Тогда
		ТекстСообщения = "Веб-хуки успешно зарегистрировано";
	Иначе
		ТекстСообщения = "Ошибка регистрации Веб-хуков: " + СтрСоединить(Результат.Ошибки, Символы.ПС);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИдентификаторМонобанкНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Отказ = Ложь;
	
	Если ПустаяСтрока(КлючАпиМонобанк) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не указан ключ АПИ, по короторому получается информация",,,,Отказ);
	КонецЕсли;
	
	Если Не Отказ Тогда
		ДанныеЗапроса = U_ЗагрузкаВыгрузкаДанных.ПолучитьДанныеСчетовМоноБанк(КлючАпиМонобанк);
		
		Если ДанныеЗапроса.Успех Тогда
			Если ДанныеЗапроса.Данные.accounts.Количество() = 0 Тогда
				СтрокаОшибки = "Нет данных о зарегистрированных счетах.";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаОшибки);	
				
			ИначеЕсли ДанныеЗапроса.Данные.accounts.Количество() = 1 Тогда
				ИдентификаторМонобанк = ДанныеЗапроса.Данные.accounts[0].id;				
				
			Иначе
				ШаблонПредставления = "Баланс: %1 %2, Кредитный лимит: %3, Тип: %4, %5";
				
				СписокЗначений = Новый СписокЗначений;
				Для Каждого ЭлементСчета Из ДанныеЗапроса.Данные.accounts Цикл
					СтрокаПредставления = СтрШаблон(ШаблонПредставления,
											Формат(ЭлементСчета.balance / 100, "ЧДЦ=2; ЧН=; ЧГ="),
											ПолучитьВалютуПоКоду(ЭлементСчета.currencyCode),
											Формат(ЭлементСчета.creditLimit / 100, "ЧДЦ=2; ЧН=; ЧГ="),
											ЭлементСчета.type,
											ЭлементСчета.maskedPan[0]);
											
					СписокЗначений.Добавить(ЭлементСчета.id, СтрокаПредставления);
				КонецЦикла;
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораСчетаДляСопоставленияМоноБанк", ЭтотОбъект, Новый Структура);
				ПоказатьВыборИзСписка(ОписаниеОповещения, СписокЗначений, ИдентификаторМонобанк);
			КонецЕсли;
		Иначе
			СтрокаОшибки = "Получение данных неудачно:" + Символы.ПС + СтрСоединить(ДанныеЗапроса.Ошибки, Символы.ПС);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаОшибки);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСчетаДляСопоставленияМоноБанк(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт 
	Если Не ВыбранныйЭлемент = Неопределено Тогда
		ИдентификаторМонобанк = ВыбранныйЭлемент.Значение;				
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолучитьВыписку(Команда)
	U_ЗагрузкаВыгрузкаДанных.ПолучитьВыпискуМоноБанк();
КонецПроцедуры

#КонецОбласти

#Область ВспомогательныеФункцииПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВалютуПоКоду(КодВалюты)
	Результат = Справочники.Валюты.НайтиПоКоду(Строка(КодВалюты));
	Если Результат = Неопределено Тогда
		Результат = КодВалюты;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти





