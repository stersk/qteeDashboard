#Область Монобанк

Функция ПолучитьПараметрыВызоваМонобанк()
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ДатаПоследнейСинхронизации", 	U_ОнлайнСтатистикаПовтИсп.ДатаПоследнейЗагрузки_Получить());
	СтруктураДанных.Вставить("Идентификатор", 				U_ОнлайнСтатистикаПовтИсп.Идентификатор_Получить());
	СтруктураДанных.Вставить("Пароль", 						U_ОнлайнСтатистикаПовтИсп.ПарольБанк_Получить());
	
	Возврат СтруктураДанных;
КонецФункции

Функция ПолучитьПараметрыЗапросаМонобанк(АдресРесурса, КлючАпи = "") 
	СтруктураПараметровЗапроса = Новый Структура;
	СтруктураПараметровЗапроса.Вставить("Сервер", 				"api.monobank.ua");
	СтруктураПараметровЗапроса.Вставить("АдресРесурса", 		АдресРесурса);
	СтруктураПараметровЗапроса.Вставить("ЗащищенноеСоединение", Новый ЗащищенноеСоединениеOpenSSL);
	
	Если Не ПустаяСтрока(КлючАпи) Тогда
		Заголовки = Новый Соответствие;
		Заголовки.Вставить("X-Token", КлючАпи);
		
		СтруктураПараметровЗапроса.Вставить("Заголовки", Заголовки);
	КонецЕсли;
	
	Возврат СтруктураПараметровЗапроса;
КонецФункции
	
Функция ПолучитьДанныеСчетовМоноБанк(КлючАпи) Экспорт 
	ПараметрыЗапроса = ПолучитьПараметрыЗапросаМонобанк("/personal/client-info", КлючАпи);
	РезультатЗапроса = U_ПроцедурыРаботыСHTTP.Получить(ПараметрыЗапроса, "JSON");
	
	Возврат РезультатЗапроса;
КонецФункции

Функция УстановитьВебХукМоноБанк(КлючАпи) Экспорт
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("webHookUrl", U_ОнлайнСтатистикаПовтИсп.АдресВебХукаМонобанк_Получить());
	
	ПараметрыЗапроса = ПолучитьПараметрыЗапросаМонобанк("/personal/webhook", КлючАпи);
	РезультатЗапроса = U_ПроцедурыРаботыСHTTP.ОтправитьДляОбработки(ПараметрыЗапроса, СтруктураДанных, "JSON", "JSON");
	
	РезультатЗапроса.Удалить("HTTPОтвет");
	
	Возврат РезультатЗапроса;
КонецФункции

Процедура ПолучитьВыпискуМоноБанк() Экспорт 
	Параметры = ПолучитьПараметрыВызоваМонобанк();
	
	Если ПустаяСтрока(Параметры.Пароль) Тогда
		Возврат;
	КонецЕсли;
	
	//Отримання виписки за час від {from} до {to} часу в секундах в форматі Unix time Максимальний час за який можливо отримувати 
	//виписку 31 доба + 1 година (2682000 секунд) Обмеження на використання функції не частіше ніж 1 раз у 60 секунд.

	НачалопПериода = ?(ЗначениеЗаполнено(Параметры.ДатаПоследнейСинхронизации), Параметры.ДатаПоследнейСинхронизации, ТекущаяДата());
	КонецПериода   = НачалопПериода;
	
	ДатаНачала    = НачалоДня(НачалопПериода) - 2505600; // - 29 дней
	ДатаОкончания = КонецДня(КонецПериода);
	
	АдресЗапроса = СтрШаблон("/personal/statement/%1/%2/%3",
						СокрЛП(Параметры.Идентификатор),
						Формат(ПолучитьUnixTimeStampИзДаты(ДатаНачала), "ЧЦ=; ЧГ="),
						Формат(ПолучитьUnixTimeStampИзДаты(ДатаОкончания), "ЧЦ=; ЧГ="));
		
	ПараметрыЗапроса = ПолучитьПараметрыЗапросаМонобанк(АдресЗапроса, Параметры.Пароль);
			
	РезультатОтправки = U_ПроцедурыРаботыСHTTP.Получить(ПараметрыЗапроса, "JSON");
	
	//U_РегистрацияРаботыПользователей.ДобавитьЗаписьЖурналаРегистрации("Информация_Справочник_БанковскиеСчетаОрганизаций.Получение выписки", "Справочник.БанковскиеСчетаОрганизаций", Параметры.БанковскийСчет, , , "Период " + Формат(НачалопПериода, "ДФ=dd.MM.yyyy; ДЛФ=D") + "-" + Формат(КонецПериода, "ДФ=dd.MM.yyyy; ДЛФ=D") + ": " + ?(РезультатОтправки.Успех, 200, 403)); 
	
	Если РезультатОтправки.Успех Тогда
		Индекс = РезультатОтправки.Данные.ВГраница();
		
		Пока Индекс >= 0 Цикл 
			ОбработатьПлатежВыпискиМоноБанк(РезультатОтправки.Данные[Индекс]);
			
			Индекс = Индекс - 1;
		КонецЦикла;
		
		Параметры.ДатаПоследнейСинхронизации = ?(НачалоДня(КонецПериода) < НачалоДня(ТекущаяДата()), КонецДня(КонецПериода) + 1, ТекущаяДата());
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьПлатежВыпискиМоноБанк(Элемент)
	Дата = ПолучитьДатуИзUnixTimeStamp(Элемент.time);
	
	Сумма             = Элемент.amount / 100;
	Комиссия		  = Элемент.commissionRate / 100;
	НазначениеПлатежа = Элемент.description;
	Код               = Элемент.id;
	
	Если Сумма = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	НазначениеПлатежа = СтрЗаменить(НазначениеПлатежа, "&#38;quot;", "&quot;");
	НазначениеПлатежа = U_ПроцедурыРаботыСHTML.ПолучитьПредставлениеСтрокиHTML(НазначениеПлатежа);
		
	ПараметрыДокумента = СоздатьПараметрыДокумента();
	ПараметрыДокумента.Дата       = Дата;
	ПараметрыДокумента.Номер      = Код;
	ПараметрыДокумента.Сумма      = Сумма;
	ПараметрыДокумента.Комиссия   = Комиссия;
	ПараметрыДокумента.Назначение = НазначениеПлатежа;
	
	СоздатьДокумент(ПараметрыДокумента); 
КонецПроцедуры

Процедура ОбработатьВебХукНовогоПлатежаМоноБанк(ДанныеПлатежа) Экспорт
	ИдентификаторАккаунта = U_ОнлайнСтатистикаПовтИсп.Идентификатор_Получить();
	Если ИдентификаторАккаунта = ДанныеПлатежа.data.account Тогда
		ОбработатьПлатежВыпискиМоноБанк(ДанныеПлатежа.data.statementItem);
	КонецЕсли;
КонецПроцедуры

Функция СоздатьПараметрыДокумента(Вид = "", ОпределятьУникальностьПоНазначению = Ложь) Экспорт 
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("Дата",                               ТекущаяДата());
	ПараметрыДокумента.Вставить("Номер",                              "1");
	ПараметрыДокумента.Вставить("Счет",                               "");
	ПараметрыДокумента.Вставить("Сумма",                              0);
	ПараметрыДокумента.Вставить("Комиссия",                           0);
	ПараметрыДокумента.Вставить("Назначение",                         "");
		
	Возврат ПараметрыДокумента;
КонецФункции

Процедура СоздатьДокумент(ПараметрыДокумента)
	НайденныйДокумент = Документы.U_ОнлайнПлатеж.НайтиПоНомеру(ПараметрыДокумента.Номер);
	Если Не ЗначениеЗаполнено(НайденныйДокумент) Тогда
		ДокументОбъект = Документы.U_ОнлайнПлатеж.СоздатьДокумент();
		ДокументОбъект.Дата  		= ПараметрыДокумента.Дата;
		ДокументОбъект.Номер 		= ПараметрыДокумента.Номер;
		ДокументОбъект.Сумма 		= ПараметрыДокумента.Сумма;
		ДокументОбъект.Комиссия 	= ПараметрыДокумента.Комиссия;
		ДокументОбъект.Назначение 	= ПараметрыДокумента.Назначение;
		
		ДокументОбъект.Записать();
	КонецЕсли;
КонецПроцедуры

Функция ПолучитьUnixTimeStampИзДаты(Дата) 
	Возврат УниверсальноеВремя(Дата) - Дата(1970, 1, 1);
КонецФункции

Функция ПолучитьДатуИзUnixTimeStamp(UnixTimeStamp)  
	Возврат МестноеВремя(Дата(1970, 1, 1) + Число(UnixTimeStamp)); 
КонецФункции

#КонецОбласти

#Область СерверСтатистики

Функция ВыгрузитьСтатистику() Экспорт 
	Результат = U_ПроцедурыРаботыСФункциями.ПолучитьСтруктуруВозврата();
	
	ПромежуточныйРезультат = ВыгрузитьОтгрузки();
	Если Не ПромежуточныйРезультат.Успех Тогда
		ОбработатьРезультатСОшибками(Результат, ПромежуточныйРезультат);
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура ОбработатьРезультатСОшибками(ОсновнойРезультат, РезультатСОшибками)
	ОсновнойРезультат.Успех = Ложь;
	Для Каждого Ошибка Из РезультатСОшибками.Ошибки Цикл 
		Если Не ПустаяСтрока(Ошибка) Тогда
			ОсновнойРезультат.Ошибки.Добавить(Ошибка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ВыгрузитьОтгрузки() 
	МассивДанныхОтгрузок = Новый Массив;
	МассивВыгруженныхДанных = Новый Массив;
	
	СтруктураОтвет = Новый Структура("id, date, sum, customer, phone, address, declarationNumber, deliveryService, status");
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Узел", ПланыОбмена.U_ВыгрузкаОнлайнСтатистики.ПолучитьУзел());
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказКлиента.U_ПолучательФамилия КАК U_ПолучательФамилия,
	|	ЗаказКлиента.U_ПолучательИмя КАК U_ПолучательИмя,
	|	ЗаказКлиента.U_ПолучательОтчество КАК U_ПолучательОтчество,
	|	ЗаказКлиента.U_ПолучательТелефон КАК U_ПолучательТелефон,
	|	ЗаказКлиента.U_СлужбаДоставки КАК U_СлужбаДоставки,
	|	ЗаказКлиента.U_АдресДоставкиОбласть КАК U_АдресДоставкиОбласть,
	|	ЗаказКлиента.U_НомерДекларации КАК U_НомерДекларации,
	|	ЗаказКлиента.U_СтатусДоставки КАК U_СтатусДоставки,
	|	ЗаказКлиента.U_АдресДоставкиНаселенныйПункт КАК U_АдресДоставкиНаселенныйПункт,
	|	ЗаказКлиента.U_АдресДоставкиОтделениеАдрес КАК U_АдресДоставкиОтделениеАдрес,
	|	ЗаказКлиента.Дата КАК Дата,
	|	ЗаказКлиента.СуммаДокумента КАК СуммаДокумента,
	|	ЗаказКлиента.Партнер КАК Партнер,
	|	ЗаказКлиента.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ЗаказКлиента КАК ЗаказКлиента
	|ГДЕ
	|	ЗаказКлиента.Ссылка В
	|			(ВЫБРАТЬ
	|				ЗаказКлиентаИзменения.Ссылка КАК Ссылка
	|			ИЗ
	|				Документ.ЗаказКлиента.Изменения КАК ЗаказКлиентаИзменения
	|			ГДЕ
	|				ЗаказКлиентаИзменения.Узел = &Узел)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл		
		Если ПустаяСтрока(Выборка.U_НомерДекларации) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураВозврат = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(СтруктураОтвет);
		МассивДанныхОтгрузок.Добавить(СтруктураВозврат);
		
		МассивСтрокКлиент = Новый Массив;
		Если Не ПустаяСтрока(Выборка.U_ПолучательФамилия) Тогда
			МассивСтрокКлиент.Добавить(Выборка.U_ПолучательФамилия);
		КонецЕсли;
		
		Если Не ПустаяСтрока(Выборка.U_ПолучательИмя) Тогда
			МассивСтрокКлиент.Добавить(Выборка.U_ПолучательИмя);
		КонецЕсли;

		Если Не ПустаяСтрока(Выборка.U_ПолучательОтчество) Тогда
			МассивСтрокКлиент.Добавить(Выборка.U_ПолучательОтчество);
		КонецЕсли;
		
		Если Не МассивСтрокКлиент.Количество() Тогда
			МассивСтрокКлиент.Добавить(Строка(Выборка.Партнер));
		КонецЕсли;		
		Клиент = СтрСоединить(МассивСтрокКлиент, " ");
		
		МассивСтрокАдрес = Новый Массив;
		Если Не ПустаяСтрока(Выборка.U_АдресДоставкиОбласть) Тогда
			МассивСтрокАдрес.Добавить(Выборка.U_АдресДоставкиОбласть);
		КонецЕсли;

		Если Не ПустаяСтрока(Выборка.U_АдресДоставкиНаселенныйПункт) Тогда
			МассивСтрокАдрес.Добавить(Выборка.U_АдресДоставкиНаселенныйПункт);
		КонецЕсли;

		Если Не ПустаяСтрока(Выборка.U_АдресДоставкиОтделениеАдрес) Тогда
			МассивСтрокАдрес.Добавить(Выборка.U_АдресДоставкиОтделениеАдрес);
		КонецЕсли;
		Адрес = СтрСоединить(МассивСтрокАдрес, ", ");
				
		СтруктураВозврат.id = Строка(Выборка.Ссылка.УникальныйИдентификатор());
		СтруктураВозврат.date = Выборка.Дата;
		СтруктураВозврат.sum = Выборка.СуммаДокумента;
		СтруктураВозврат.customer = Клиент;
		СтруктураВозврат.phone = Выборка.U_ПолучательТелефон;
		СтруктураВозврат.address = Адрес;
		СтруктураВозврат.declarationNumber = Выборка.U_НомерДекларации;
		СтруктураВозврат.deliveryService = Выборка.U_СлужбаДоставки;
		СтруктураВозврат.status = Выборка.U_СтатусДоставки;
	КонецЦикла;
	
	РезультатОтсылки = ОтправитьДанныеСтатистики("services/shipment/save-all", МассивДанныхОтгрузок); 
	Если РезультатОтсылки.Успех Тогда
		ПланыОбмена.УдалитьРегистрациюИзменений(ПланыОбмена.U_ВыгрузкаОнлайнСтатистики.ПолучитьУзел(), Метаданные.Документы.ЗаказКлиента);
	КонецЕсли;
	
	Возврат РезультатОтсылки;
КонецФункции

Функция ОтправитьДанныеСтатистики(ТочкаВхода, Данные)
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Сервер", 		U_ОнлайнСтатистикаПовтИсп.АдресСерверСтатистики_Получить());
	ПараметрыЗапроса.Вставить("Порт", 			U_ОнлайнСтатистикаПовтИсп.ПортСерверСтатистики_Получить());
	ПараметрыЗапроса.Вставить("АдресРесурса", 	ТочкаВхода);
	ПараметрыЗапроса.Вставить("Пользователь", 	U_ОнлайнСтатистикаПовтИсп.ПользовательСерверСтатистики_Получить());
	ПараметрыЗапроса.Вставить("Пароль", 		U_ОнлайнСтатистикаПовтИсп.ПарольСерверСтатистики_Получить());
	ПараметрыЗапроса.Вставить("Заголовки", 		Заголовки);
	
	Если U_ОнлайнСтатистикаПовтИсп.HTTPSСерверСтатистики_Получить() Тогда
		ПараметрыЗапроса.Вставить("ЗащищенноеСоединение", Новый ЗащищенноеСоединениеOpenSSL);
	КонецЕсли;
	
	Результат = U_ПроцедурыРаботыСHTTP.ОтправитьДляОбработки(ПараметрыЗапроса, Данные, "JSON", "JSON");
	Возврат Результат;
КонецФункции

Функция ПолучитьДанныеССервераСтатистики(ТочкаВхода) Экспорт 
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("Сервер", 		U_ОнлайнСтатистикаПовтИсп.АдресСерверСтатистики_Получить());
	ПараметрыЗапроса.Вставить("Порт", 			U_ОнлайнСтатистикаПовтИсп.ПортСерверСтатистики_Получить());
	ПараметрыЗапроса.Вставить("АдресРесурса", 	ТочкаВхода);
	ПараметрыЗапроса.Вставить("Пользователь", 	U_ОнлайнСтатистикаПовтИсп.ПользовательСерверСтатистики_Получить());
	ПараметрыЗапроса.Вставить("Пароль", 		U_ОнлайнСтатистикаПовтИсп.ПарольСерверСтатистики_Получить());
	ПараметрыЗапроса.Вставить("Заголовки", 		Заголовки);

	Если U_ОнлайнСтатистикаПовтИсп.HTTPSСерверСтатистики_Получить() Тогда
		ПараметрыЗапроса.Вставить("ЗащищенноеСоединение", Новый ЗащищенноеСоединениеOpenSSL);
	КонецЕсли;
	
	Результат = U_ПроцедурыРаботыСHTTP.Получить(ПараметрыЗапроса, "JSON");
	Возврат Результат;
КонецФункции

#КонецОбласти
